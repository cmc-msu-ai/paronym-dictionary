buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:0.4'
    }
}

apply plugin: 'java'
apply plugin: 'application'

apply plugin: 'eclipse'
apply plugin: 'idea'

mainClassName = 'gui.GuiCtrl'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.intellij:forms_rt:7.0.3'
    compile 'org.hsqldb:hsqldb:1.8.0.10'
    testCompile 'junit:junit:4.+'
}

// IDE-specific settings

idea.project.ipr {
    withXml { provider ->
        provider.node.appendNode('component', [name: 'uidesigner-configuration']).appendNode('option', [name: 'INSTRUMENT_CLASSES', value: 'false'])
    }
}

// DB management tasks

import de.undercouch.gradle.tasks.download.Download

task downloadDb(type: Download) {
	src 'https://s3-eu-west-1.amazonaws.com/cmcmsuai/paronym-dictionary/db/paronym.zip'
	dest 'db'
	onlyIfNewer true
}

task extractDb(type: Sync, dependsOn: 'downloadDb') {
    from zipTree('db/paronym.zip')
    into 'db/paronym'
}

run.dependsOn extractDb
distTar.dependsOn extractDb
distZip.dependsOn extractDb

applicationDistribution.from('db/paronym') {
    into 'db/paronym'
}
